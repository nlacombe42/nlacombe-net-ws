buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'io.spring.dependency-management' version '1.0.6.RELEASE'
    id 'org.springframework.boot' version '2.2.6.RELEASE'
}

apply plugin: 'docker'

group = 'net.nlacombe'
version = '1.0.1'

def springCloudVersion = 'Hoxton.SR4'
def junitVersion = '5.3.1'
def jacksonVersion = '2.9.6'

project.ext.gcloudBuildDir = file('build/gcloud-build/')
project.ext.gcloudProjectId = 'nlacombe-net-prod'

String gcloudDockerImageUrl

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-starter-parent:${springCloudVersion}"
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

dependencies {
    compileOnly 'org.springframework.boot:spring-boot-devtools'

    implementation 'net.nlacombe:auth-lib:12.0.1'

    implementation 'org.springframework.cloud:spring-cloud-gcp-starter'
    implementation 'org.springframework.cloud:spring-cloud-gcp-starter-sql-mysql'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.flywaydb:flyway-core:6.4.1'
	implementation 'javax.inject:javax.inject:1'
	implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'com.vladsch.flexmark:flexmark-all:0.61.28'
    implementation 'org.apache.commons:commons-lang3:3.10'
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"

	runtimeOnly 'ch.qos.logback:logback-classic:1.2.3'
    runtimeOnly 'mysql:mysql-connector-java:8.0.18'

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.assertj:assertj-core:3.16.1'
    testImplementation 'org.mockito:mockito-core:3.3.3'
    testImplementation 'io.jsonwebtoken:jjwt-api:0.11.2'
    testImplementation 'org.jsoup:jsoup:1.13.1'
    testImplementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
	mavenCentral()
	mavenLocal()
}

wrapper {
	gradleVersion = '7.1.1'
}

test {
    useJUnitPlatform()
}

idea {
	module {
		downloadJavadoc = true
		downloadSources = false
	}
}

task buildDockerImage(type: Docker, dependsOn: assemble) {
	tag = "${project.name}"
	dockerfile = file('src/main/docker/Dockerfile')
	doFirst {
		copy {
			from jar
			from file('src/main/docker/')
			into stageDir
		}
	}
}

task buildGcloudDockerImage(type: Exec, dependsOn: [assemble, check]) {
	def buildStartTime = new Date()
	gcloudDockerImageUrl = getGcloudDockerImageUrl(buildStartTime)

	workingDir project.gcloudBuildDir
	commandLine 'gcloud', 'builds', 'submit', '--tag', gcloudDockerImageUrl

	doFirst {
		copy {
			from jar
			from file('src/main/docker/')
			into project.gcloudBuildDir
		}
	}
}

def getGcloudDockerImageUrl(date) {
	def version = project.version + '-' + getDateTimestamp(date)

	return "gcr.io/${project.gcloudProjectId}/${project.name}:${version}"
}

def static getDateTimestamp(date) {
	return date.format('yyyyMMddHHmmss')
}
